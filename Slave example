#include <Wire.h>
#include <Indio.h>
#include <SimpleModbusSlave.h>
#include <UC1701.h>
static UC1701 lcd;

/*
   this example receives 4 registers from the master [0-3]
   and sets 4 registers [4-7]

   SimpleModbusSlaveV10 supports function 3, 6 & 16.

   function 3: READ_HOLDING_REGISTERS    **** this example uses this function [4-7]
   function 6: PRESET_SINGLE_REGISTER
   function 16: PRESET_MULTIPLE_REGISTERS **** this example uses this function [0-3]

   The modbus_update() method updates the holdingRegs register array and checks
   communication.

   Note:
   The Arduino serial ring buffer is 64 bytes or 32 registers.
   Most of the time you will connect the arduino to a master via serial
   using a MAX485 or similar.

   In a function 3 request the master will attempt to read from your
   slave and since 5 bytes is already used for ID, FUNCTION, NO OF BYTES
   and two BYTES CRC the master can only request 58 bytes or 29 registers.

   In a function 16 request the master will attempt to write to your
   slave and since a 9 bytes is already used for ID, FUNCTION, ADDRESS,
   NO OF REGISTERS, NO OF BYTES and two BYTES CRC the master can only write
   54 bytes or 27 registers.

   Using a USB to Serial converter the maximum bytes you can send is
   limited to its internal buffer which differs between manufactures.
*/

// used to toggle the receive/transmit pin on the driver
#define TxEnablePin 9                                                           // INDUSTRUINO RS485

#define   baud 9600   // use 9600 on D21G
#define   SlaveID 2
#define   HOLDING_REGS_SIZE 8

// Using the enum instruction allows for an easy method for adding and
// removing registers. Doing it this way saves you #defining the size
// of your slaves register array each time you want to add more registers
// and at a glimpse informs you of your slaves register layout.

//////////////// registers of your slave ///////////////////
/*
enum
{
  // just add or remove registers and your good to go...
  // The first register starts at address 0

  HOLDING_REGS_SIZE // leave this one
  // total number of registers for function 3 and 16 share the same register array
  // i.e. the same address space
};
*/

unsigned int holdingRegs[HOLDING_REGS_SIZE]; // function 3 and 16 register array
////////////////////////////////////////////////////////////

void setup()
{

  lcd.begin();
  lcd.clear();
  lcd.setCursor(1, 2);
  lcd.print("hello Industruino!");
  lcd.setCursor(1, 4);
  lcd.print("Modbus RTU Slave");

  analogWrite(26, 100);  // LCD backlight

  Indio.digitalMode(1, OUTPUT);
  Indio.digitalMode(2, OUTPUT);
  Indio.digitalMode(3, OUTPUT);
  Indio.digitalMode(4, OUTPUT);

  Indio.digitalMode(7, INPUT);
  Indio.digitalMode(8, INPUT);

  /* parameters(HardwareSerial* SerialPort,
                long baudrate,
      unsigned char byteFormat,
                unsigned char ID,
                unsigned char transmit enable pin,
                unsigned int holding registers size,
                unsigned int* holding register array)
  */

  /* Valid modbus byte formats are:
     SERIAL_8N2: 1 start bit, 8 data bits, 2 stop bits
     SERIAL_8E1: 1 start bit, 8 data bits, 1 Even parity bit, 1 stop bit
     SERIAL_8O1: 1 start bit, 8 data bits, 1 Odd parity bit, 1 stop bit

     You can obviously use SERIAL_8N1 but this does not adhere to the
     Modbus specifications. That said, I have tested the SERIAL_8N1 option
     on various commercial masters and slaves that were suppose to adhere
     to this specification and was always able to communicate... Go figure.

     These byte formats are already defined in the Arduino global name space.
  */

  modbus_configure(&Serial, baud, SERIAL_8N2, SlaveID, TxEnablePin, HOLDING_REGS_SIZE, holdingRegs);

  // modbus_update_comms(baud, byteFormat, id) is not needed but allows for easy update of the
  // port variables and slave id dynamically in any function.
  modbus_update_comms(baud, SERIAL_8N2, SlaveID);

}

void loop()
{
  // modbus_update() is the only method used in loop(). It returns the total error
  // count since the slave started. You don't have to use it but it's useful
  // for fault finding by the modbus master.
  // the library manual suggests not using long delays but an interval instead
//  unsigned long moment = millis();
  modbus_update();
//  unsigned long interval = millis()-moment;
//  if (interval > 0) Serial.println(interval);
  
  holdingRegs[6] = Indio.digitalRead(7);    // set register according to button on Slave
  holdingRegs[7] = Indio.digitalRead(8);    // set register according to button on Slave

  for (int u = 0; u < 4; u++) {
    Indio.digitalWrite(u + 1, holdingRegs[u]);  // switch on LED according to Master request
  }

  /* Note:
     The use of the enum instruction is not needed. You could set a maximum allowable
     size for holdinRegs[] by defining HOLDING_REGS_SIZE using a constant and then access
     holdingRegs[] by "Index" addressing.
     I.e.
     holdingRegs[0] = analogRead(A0);
     analogWrite(LED, holdingRegs[1]/4);
  */

}
